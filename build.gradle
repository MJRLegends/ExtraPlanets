def minecraft_version="1.10.2"
def jei_version="3.14.7.416"
def ccl_version="2.5.7.248"
def nei_version="2.1.3.220"
def ccc_version="2.4.1.102"
def version_mjrlegendslib="1.10.2-1.1.3"
def version_planetprogression="1.10.2-0.3.5"
def version_galacticraft="1.10.2-4.0.1.178"
def version_mekanism="1.10.2-9.2.4.103"
def version_ic2="2.6.99-ex110"
def version_cofh_core="1.10.2-4.1.12.17"

// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.1.0"
    }
}

repositories {
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
	maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
	maven {
	    name 'Redstone Flux'
	    url = "http://maven.covers1624.net"
    }
	maven {
	    name 'IC2'
	    url = "http://maven.ic2.player.to"
    }
	maven {
	    name 'MJRLegends'
	    url = "https://maven.mjrlegends.com/"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

version = "1.10.2-1.2.7"
group= "com.mjr.extraplanets" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ExtraPlanets"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.10.2-12.18.3.2511"
    runDir = "run"
    mappings = "snapshot_20161111"
    makeObfSourceJar = true // an Srg named sources jar is made by default. uncomment this to disable.
}

dependencies {
    compile "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    compile "codechicken:CodeChickenCore:${minecraft_version}-${ccc_version}:deobf"
    compile "codechicken:NotEnoughItems:${minecraft_version}-${nei_version}:deobf"
    compile "codechicken:CodeChickenLib:${minecraft_version}-${ccl_version}:deobf"
	compile "net.industrial-craft:industrialcraft-2:${version_ic2}:api"
	deobfCompile "com.mjr.mjrlegendslib:MJRLegendsLib:${version_mjrlegendslib}:dev"
	compile "com.mjr.planetprogression:PlanetProgression:${version_planetprogression}:api"
	compile files (
		"Galacticraft-API-${version_galacticraft}.jar",
		"GalacticraftCore-Dev-${version_galacticraft}.jar",
		"Galacticraft-Planets-Dev-${version_galacticraft}.jar",
		"MicdoodleCore-Dev-${version_galacticraft}.jar",
		"Mekanism-${version_mekanism}.jar",
		"CoFHCore-${version_cofh_core}-universal.jar"
	)
	runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    runtime "codechicken:CodeChickenCore:${minecraft_version}-${ccc_version}:deobf"
    runtime "codechicken:NotEnoughItems:${minecraft_version}-${nei_version}:deobf"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'     
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
	//manifest {
    //    attributes.put("FMLAT", "extraplanets_at.cfg")
    //    attributes.put("FMLCorePluginContainsFMLMod", "true")
    //}
}

task apiJar(type: Jar) {
    from(sourceSets.main.output) {
        include "com/mjr/extraplanets/api/**"
    }
	from(sourceSets.main.java) {
        include "com/mjr/extraplanets/api/**"
    }
    classifier = 'api'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

artifacts {
    archives apiJar, deobfJar
}

task ("uploadMaven"){
	apply plugin: 'maven'
	uploadArchives {
		repositories {
			mavenDeployer {
				if (project.hasProperty("local_maven")) {
					repository(url: "file://${local_maven}")
				}
			}
		}
	}
}

curseforge {
    if (project.hasProperty('curseforge_key')){
		apiKey = project.findProperty("curseforge_key")
		project {
			id = "241291"
			releaseType = "release"
			addGameVersion "${minecraft_version}"
			mainArtifact(jar) {
				relations {
					requiredLibrary 'mjrlegendslib'
				}
			}
			addArtifact(apiJar)
			addArtifact(deobfJar)
		}
	}
}

build.dependsOn signJar
tasks.build.dependsOn signJar
tasks.curseforge.dependsOn build